import groovy.json.JsonSlurper

def fileName = 'app-credentials.json'
def appStorePassword = (new JsonSlurper().parseText(file(fileName).text)).storePassword
def appKeyAlias = (new JsonSlurper().parseText(file(fileName).text)).keyAlias
def appKeyPassword = (new JsonSlurper().parseText(file(fileName).text)).keyPassword

apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

android {

    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    defaultConfig {
        applicationId "com.ringov.yamblzweather"
        minSdkVersion 17
        targetSdkVersion 25
        versionCode 4
        versionName "0.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        
        config {
            storeFile file('weather_key.jks')
            keyAlias appKeyAlias
            keyPassword appKeyPassword
            storePassword appStorePassword
        }
    }

    buildTypes {

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }

        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }

        buildTypes.each {
            it.buildConfigField "String", "BASE_URL", '"http://api.openweathermap.org/data/2.5/"'
        }
    }

    lintOptions {
        // in order to be able to set release and debug strings separately
        disable 'MissingTranslation', 'ExtraTranslation'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

ext {
    espressoVersion = '2.2.2'
    junitVersion = '4.12'
    leakCanaryVersion = '1.5.1'
    mockitoVersion = '2.7.22'
    junitVersion = '4.12'

    supportLibVersion = '25.3.1'
    architectureComponentsVersion = '1.0.0-alpha5'

    butterknifeVersion = '8.7.0'
    daggerVersion = '2.11'
    retrofitVersion = '2.3.0'
    rxJavaVersion = '2.1.1'
    rxAndroidVersion = '2.0.1'
    rxBindingVersion = '2.0.0'
    androidJobVersion = '1.1.11'
    timberVersion = '4.5.1'
}

dependencies {

    compile fileTree(include: ['*.jar'], dir: 'libs')

    androidTestCompile("com.android.support.test.espresso:espresso-core:$espressoVersion", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    testCompile "junit:junit:$junitVersion"
    testCompile "org.mockito:mockito-core:${mockitoVersion}"

    //leak canary
    debugCompile "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"

    // Support lib
    compile "com.android.support:appcompat-v7:$supportLibVersion"
    compile "com.android.support:design:$supportLibVersion"
    compile "com.android.support:preference-v14:$supportLibVersion"
    compile "com.android.support:preference-v7:$supportLibVersion"

    // architecture components
    compile "android.arch.lifecycle:runtime:$architectureComponentsVersion"
    compile "android.arch.lifecycle:extensions:$architectureComponentsVersion"
    annotationProcessor "android.arch.lifecycle:compiler:$architectureComponentsVersion"
    compile "android.arch.persistence.room:runtime:$architectureComponentsVersion"
    annotationProcessor "android.arch.persistence.room:compiler:$architectureComponentsVersion"

    // butter-knife
    compile "com.jakewharton:butterknife:$butterknifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterknifeVersion"

    // dagger 2
    compile "com.google.dagger:dagger:$daggerVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"

    // retrofit 2
    compile "com.squareup.retrofit2:retrofit:$retrofitVersion"
    compile "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    compile "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"

    // rx
    compile "io.reactivex.rxjava2:rxjava:$rxJavaVersion"
    compile "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"
    compile "com.jakewharton.rxbinding2:rxbinding:$rxBindingVersion"
    compile "com.jakewharton.rxbinding2:rxbinding-support-v4:${rxBindingVersion}"

    //android-job
    compile "com.evernote:android-job:$androidJobVersion"
    compile "com.google.android.gms:play-services-gcm:11.0.2"

    // Logging
    compile "com.jakewharton.timber:timber:${timberVersion}"
}
